;  stellar flux function
;    lam - wavelength (um)
;   Teff - effective temperature (K)
;     Rs - stellar radius (solar radii)
;      d - distance to star (pc)
;     AU - flag that indicates d is in AU
;  Fstar - stellar flux (W/m**2/um)
FUNCTION Fstar, lam, Teff, Rs, d, AU=au
  Rsun  = 6.958d8       ; solar radius (m)
  ds    = 3.08567d16    ; parsec (m)
  IF KEYWORD_SET( AU ) THEN ds = 1.495979d11 ; AU (m)
  lambda= 1.d-6*lam     ; wavelength (m)
  c1    = 3.7417715d-16 ; 2*pi*h*c*c (kg m**4 / s**3)   
  c2    = 1.4387769d-2  ; h*c/k (m K)
  pow   = c2/lambda/Teff
  Fs    = c1/( (lambda^5.)*(exp(pow)-1.d) )*1.d-6
  RETURN, Fs*(Rs*Rsun/d/ds)^2.
END

;  planetary flux functiom
;      A - planetary geometric albedo
;    Phi - planetary phase function
;  Fstar - stellar flux (W/m**2/um)
;     Rp - planetary radius (Earth radii)
;      d - distance (pc)
;     au - flag that indicates d is in AU
;  Fplan - planetary flux (W/**2/um)
FUNCTION Fplan, A, Phi, Fstar, Rp, d, AU=au
  Re    = 6.371e6       ; radius of Earth (m)
  ds    = 3.08567d16    ; parsec (m)
  IF KEYWORD_SET( AU ) THEN ds = 1.495979d11 ; AU (m)
  RETURN, A*Phi*Fstar*(Rp*Re/d/ds)^2.
END

;  planet-star flux ratio
;      A - planetary geometric albedo
;    Phi - planetary phase function
;     Rp - planetary radius (Earth radii)
;      r - orbital distance (au)
;   FpFs - planet-star flux ratio
FUNCTION FpFs, A, Phi, Rp, r
  Re = 6.371e6 ; radius of Earth (m)
  ds = 1.495979d11 ; AU (m)
  RETURN, A*Phi*(Rp*Re/r/ds)^2.
end

;  planet photon count rate
;      q - quantum efficiency
;      X - size of photometric aperture (lambda/D)
;      T - system throughput
;    lam - wavelength (um)
;   dlam - spectral element width (um)
;  Fplan - planetary flux (W/m**2/um)
;      D - telescope diameter (m)
;  cplan - planet photon count rate (s**-1)
FUNCTION cplan, q, X, T, lam, dlam, Fplan, D
  hc  = 1.986446d-25 ; h*c (kg*m**3/s**2)
  fpa = 1. - BESELJ(!DPI*X,0)^2. - BESELJ(!DPI*X,1)^2. ;fraction of power in Airy disk to X*lambda/D
  RETURN, !DPI*q*fpa*T*(lam*1.d-6/hc)*dlam*Fplan*(D/2)^2.
END

;  zodiacal light count rate
;      q - quantum efficiency
;      X - size of photometric aperture (lambda/D)
;      T - system throughput
;    lam - wavelength (um)
;   dlam - spectral element width (um)
;      D - telescope diameter (m)
;    MzV - zodiacal light surface brightness (mag/arcsec**2)
;   /SUN - set to use WMO solar spectrum
;  czodi - zodiacal light photon count rate (s**-1)
FUNCTION czodi, q, X, T, lam, dlam, D, MzV, SUN=sun
  hc    = 1.986446d-25 ; h*c (kg*m**3/s**2)
  F0V   = 3.6d-8       ; zero-mag V-band flux (W/m**2/um)
  FsolV = 1.86d+3      ; Solar V-band flux at 1 AU
  IF KEYWORD_SET(sun) THEN BEGIN
    fn    = '/Users/robinson/Work/noise/wmo_solar_spectrum.dat'
    READCOL, fn, lamsol, Fsol, SKIPLINE=32, /SILENT
    Fsol  = DEGRADE_SPEC(Fsol,lamsol,lam,DLAM=dlam) ; degrade solar spectrum (W/m**2/um)
  ENDIF ELSE BEGIN
   Teffs  = 5778.d ;Sun effective temperature
      Rs  = 1.d    ;Sun radius (in solar radii)
    Fsol  = Fstar(lam, Teffs, Rs, 1., /AU)
  ENDELSE
  rat   = DBLARR(N_ELEMENTS(lam))
  rat[*]= Fsol[*]/FsolV ; ratio of solar flux to V-band solar flux
  Omega = !DPI*(X*lam*1.d-6/D*180.*3600./!DPI)^2. ; aperture size (arcsec**2)
  RETURN, !DPI*q*T*Omega*dlam*(lam*1.d-6/hc)*(D/2)^2.*rat*F0V*10^(-Mzv/2.5)
END

;  exozodiacal light count rate
;      q - quantum efficiency
;      X - size of photometric aperture (lambda/D)
;      T - system throughput
;    lam - wavelength (um)
;   dlam - spectral element width (um)
;      D - telescope diameter (m)
;      r - orbital distance (au)
;  Fstar - host star spectrum *at 1 au* (W/m**2/um)
;    Nez - number of exozodis
;   MezV - exozodiacal light surface brightness (mag/arcsec**2)
;   /SUN - set to use WMO solar spectrum
; cezodi - exozodiacal light photon count rate (s**-1)
FUNCTION cezodi, q, X, T, lam, dlam, D, r, Fstar, Nez, MezV, SUN=sun
  hc    = 1.986446d-25 ; h*c (kg*m**3/s**2)
  F0V   = 3.6d-8       ; zero-mag V-band flux (W/m**2/um)
  FsolV = 1.86d+3      ; Solar V-band flux at 1 AU
  IF KEYWORD_SET(sun) THEN BEGIN
    fn    = '/Users/robinson/Work/noise/wmo_solar_spectrum.dat'
    READCOL, fn, lamsol, Fsol, SKIPLINE=32, /SILENT
    Fsol  = DEGRADE_SPEC(Fsol,lamsol,lam,DLAM=dlam) ; degrade solar spectrum (W/m**2/um)
  ENDIF ELSE BEGIN
   Teffs  = 5778.d ;Sun effective temperature
      Rs  = 1.d    ;Sun radius (in solar radii)
    Fsol  = Fstar(lam, Teffs, Rs, 1., /AU) ; Sun as blackbody (W/m**2/um)
  ENDELSE
  rat   = DBLARR(N_ELEMENTS(lam))
  rat[*]= Fstar[*]/FsolV ; ratio of solar flux to V-band solar flux
  Omega = !DPI*(X*lam*1.d-6/D*180.*3600./!DPI)^2. ; aperture size (arcsec**2)
  RETURN, !DPI*q*T*Omega*dlam*(lam*1.d-6/hc)*(D/2)^2.*(1./r)^2.*rat*Nez*F0V*10^(-Mezv/2.5)
END

;  speckle count rate
;      q - quantum efficiency
;      T - system throughput
;      C - design contrast
;    lam - wavelength (um)
;   dlam - spectral element width (um)
;      D - telescope diameter (m)
;  Fstar - host star spectrum at distance to system (W/m**2/um)
; cspeck - speckle photon count rate (s**-1)
FUNCTION cspeck, q, T, C, lam, dlam, Fstar, D
  hc    = 1.986446d-25 ; h*c (kg*m**3/s**2)
  RETURN, !DPI*q*T*C*dlam*Fstar*(lam*1.d-6/hc)*(D/2)^2.
END

;  dark count rate
;     De - dark count rate (s**-1)
;      X - size of photometric aperture (lambda/D)
;    lam - wavelength (um)
;      D - telescope diameter (m)
;  theta - angular size of lenslet or pixel (arcsec**2)
; DNhpix - number of pixels spectrum spread over in horizontal, for IFS
;  IMAGE - keyword set to indicate imaging mode (not IFS)
;  cdark - dark count rate (s**-1)
FUNCTION cdark, De, X, lam, D, theta, DNhpix, IMAGE=image
  Omega = !DPI*(X*lam*1.d-6/D*180.*3600./!DPI)^2. ; aperture size (arcsec**2)
  Npix  = Omega/!DPI/theta^2.
  IF ~KEYWORD_SET( IMAGE ) THEN Npix = 2*DNhpix*Npix
  RETURN, De*Npix
END

;  read noise count rate
;     Re - read noise counts per pixel
;      X - size of photometric aperture (lambda/D)
;    lam - wavelength (um)
;      D - telescope diameter (m)
;  theta - angular size of lenslet or pixel (arcsec**2)
;  Dtmax - maximum exposure time (hr)
;  IMAGE - keyword set to indicate imaging mode (not IFS)
;  cread - read count rate (s**-1)
FUNCTION cread, Re, X, lam, D, theta, DNhpix, Dtmax, IMAGE=image
  Omega = !DPI*(X*lam*1.d-6/D*180.*3600./!DPI)^2. ; aperture size (arcsec**2)
  Npix  = Omega/!DPI/theta^2.
  IF ~KEYWORD_SET( IMAGE ) THEN Npix = 2*DNhpix*Npix
  RETURN, Npix/(Dtmax*3600.)*Re
END

;  telescope thermal count rate
;      q - quantum efficiency
;      X - size of photometric aperture (lambda/D)
;    lam - wavelength (um)
;   dlam - spectral element width (um)
;      D - telescope diameter (m)
;   Tsys - telescope/system temperature (K)
;   emis - telescope/system emissivity
; ctherm - telescope thermal photon count rate (s**-1)
FUNCTION ctherm, q, X, lam, dlam, D, Tsys, emis
  hc    = 1.986446d-25  ; h*c (kg*m**3/s**2)
  c1    = 3.7417715d-16 ; 2*pi*h*c*c (kg m**4 / s**3)   
  c2    = 1.4387769d-2  ; h*c/k (m K)
  lambda= 1.d-6*lam     ; wavelength (m)
  pow   = c2/lambda/Tsys
  Bsys  = c1/( (lambda^5.)*(exp(pow)-1.d) )*1.d-6/!DPI ;system Planck function (W/m**2/um/sr)
  Omega = !DPI*(X*lam*1.d-6/D)^2. ; aperture size (sr**2)
  RETURN, !DPI*q*dlam*emis*Bsys*Omega*(lam*1.d-6/hc)*(D/2)^2.
END
